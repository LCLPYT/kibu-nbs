import work.lclpnet.build.task.GithubDeploymentTask

plugins {
	id 'fabric-loom' version '1.9-SNAPSHOT'
	id 'maven-publish'
	id 'gradle-build-utils' version '1.7.0'
    id 'com.modrinth.minotaur' version '2.4.3'
}

Properties props = buildUtils.loadProperties('publish.properties')

version = buildUtils.gitVersion()
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        url "https://repo.lclpnet.work/repository/internal"
    }
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "notica" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

sourceSets {
    api {}

    main {
        compileClasspath += api.getOutput()
        runtimeClasspath += api.getOutput()
    }

    client {
        compileClasspath += api.getOutput()
        runtimeClasspath += api.getOutput()
    }

    test {
        compileClasspath += client.getOutput() + api.getOutput()
        runtimeClasspath += client.getOutput() + api.getOutput()
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    include(modImplementation("work.lclpnet.mods.kibu:kibu-hooks:${project.kibu_hooks_version}"))
    include(modImplementation("work.lclpnet.mods.kibu:kibu-translation-api:${project.kibu_translation_api_version}"))
    include(modImplementation(apiImplementation("work.lclpnet.mods.kibu:kibu-hook-api:${project.kibu_hook_api_version}")))
    include(modImplementation("work.lclpnet.mods.kibu:kibu-access-lib:${project.kibu_access_lib_version}"))
    include(modImplementation("work.lclpnet.mods.kibu:kibu-networking-api:${project.kibu_networking_api_version}"))
    include(modImplementation('me.lucko:fabric-permissions-api:0.3.1'))

    include(implementation('org.json:json:20230227'))
    include(implementation('work.lclpnet:json-config4j:1.0.0'))

    apiImplementation(implementation('org.jetbrains:annotations:24.0.0'))

    testImplementation "net.fabricmc:fabric-loader-junit:${project.loader_version}"
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.1'
}

test {
    useJUnitPlatform()
}

afterEvaluate {
    configurations {
        testCompileClasspath.extendsFrom(minecraftCommonNamedCompile)
        testRuntimeClasspath.extendsFrom(minecraftCommonNamedRuntime)
        testCompileClasspath.extendsFrom(minecraftClientOnlyNamedCompile)
        testRuntimeClasspath.extendsFrom(minecraftClientOnlyNamedRuntime)
        testRuntimeClasspath.extendsFrom(modRuntimeClasspathClientMapped)
    }
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release.set(21)
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

[jar, sourcesJar].each {
	it.from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}

    it.from sourceSets.api.output
}

def env = System.getenv()

tasks.register("github", GithubDeploymentTask) {
    dependsOn tasks.remapJar

    config {
        token = env.get("GITHUB_TOKEN")
        repository = env.get("GITHUB_REPOSITORY")
    }

    release {
        title = "[$project.minecraft_version] Notica $project.version"
        tag = buildUtils.latestTag()
    }

    assets.add(tasks.remapJar.archiveFile.get())
}

modrinth {
    token = env.get("MODRINTH_TOKEN")

    projectId = "notica"
    versionName = "[$project.minecraft_version] Notica $project.version"
    versionType = "release"
    uploadFile = remapJar

    dependencies {
        required.project "fabric-api"
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.base.archivesName.get()

            from components.java

            pom {
                name = 'Notica'
                description = 'A fabric mod for playing .nbs files'
            }
        }
    }

    // automatically use DEPLOY_URL, DEPLOY_USER and DEPLOY_PASSWORD environment variables
    // or mavenHost, mavenUser and mavenPassword from props
    buildUtils.setupPublishRepository(repositories, props)
}